# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

pkgname=linux-api-headers
_major=4.17
_srcname=linux-${_major}
#_minor=14
#pkgver=${_major}.${_minor}
pkgver=${_major}
pkgrel=1
pkgdesc="Kernel headers sanitized for use in userspace"
arch=('any')
url="http://www.gnu.org/software/libc"
license=('GPL2')
provides=('linux-api-headers=$pkgver')
conflicts=('linux-api-headers')
source=(https://www.kernel.org/pub/linux/kernel/v4.x/linux-${_major}.tar.xz
        https://www.kernel.org/pub/linux/kernel/v4.x/linux-${_major}.tar.sign)
        #https://www.kernel.org/pub/linux/kernel/v4.x/patch-${pkgver}.xz
        #https://www.kernel.org/pub/linux/kernel/v4.x/patch-${pkgver}.sign)
md5sums=('5bb13a03274b66b56c85b26682e407d7'
         'SKIP')
         
validpgpkeys=('ABAF11C65A2970B130ABE3C479BE3E4300411886'   # Linus Torvalds
              '647F28654894E3BD457199BE38DBBDC86092693E')  # Greg Kroah-Hartman

prepare() {
  cd linux-${_major}
  #patch -p1 -i "$srcdir/patch-$pkgver" 
}

build() {
  cd linux-${_major}

  make mrproper
  make headers_check
}

package() {
  cd linux-${_major}
  make INSTALL_HDR_PATH="$pkgdir/usr" headers_install

  # use headers from libdrm
  rm -r "$pkgdir/usr/include/drm"
  
  # clean-up unnecessary files generated during install
  find "$pkgdir" \( -name .install -o -name ..install.cmd \) -delete
}
