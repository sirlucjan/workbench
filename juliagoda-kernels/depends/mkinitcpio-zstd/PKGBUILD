# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=mkinitcpio-zstd
_pkgname=mkinitcpio
pkgver=30
pkgrel=4
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url='https://github.com/archlinux/mkinitcpio'
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive' 'coreutils'
         'bash' 'diffutils' 'findutils' 'grep' 'filesystem>=2011.10-1' 'zstd' 'systemd')
makedepends=('asciidoc')
optdepends=('gzip: Use gzip compression for the initramfs image'
            'xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=('initramfs' 'initramfs-zstd' "mkinitcpio=$pkgver")
conflicts=('mkinitcpio')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$_pkgname/$_pkgname-$pkgver.tar.gz"{,.sig}
        "0001-Add-zstd-module-decompression.patch"
        "0002-Silence-messages-in-zstd-format.patch"
        "0003-Add-rm-flag-to-minimize-the-size-of-initramfs.patch"
        "0004-Merge-mkinitcpio-libalpm-scripts-into-one.patch")
install=mkinitcpio.install
sha512sums=('ef43a8ddc01697fa43bede4eebad3271bbc89efa57ba7e4e9f321e8174ff2af74d857b308c5c0f37b42a91906c91c1b022f9a651c70d5d3e9dfc8dc441a60ee4'
            'SKIP'
            '9d43c356c33e6489cd365229a4d3e75804019343e258b75459d61bb09262528383637fe9127834fb3a6a8732ef6cde90628aee7a5c6477ab124f7639195fedaf'
            'b254d9ff98075ea3b3828436e776d75d122009feb77f13101fc60dce0605e1b90aa4f589bd2b218001799546ed9e1dad3517f063a88b8222eac3ddf1fb67fd6b'
            '34abf3b5eba1092e1ffaab601979d4f0bbb8c5a432cca89ed53103195c47916e940e3812378e4f124cf8dc559d3377eeffde538750bca4f1fee1fb8dc5e21e5b'
            '20eac54a962f27751ae2e4696cb491f08a9a2d1d114d4c977ea4b9a24ed29e8a3e0243fc9d320e64d64aab88237dec836911a366e516ece0d6b3fe95cba0dc8a')
b2sums=('2001f9ac12bfa7369ad32842956cd21b2a786cd28133b6d3256e4aa77210c78ec6cac8a710ff8c2feb5a4797c1a33ce8c8c7dfabd84d7927c08bf8377e330300'
        'SKIP'
        '789c82a29e741048e6a09a1905edce1df2fd8f13c33397f5691a30cd5b376713ed65fec031284260e727da1591bb3ed317dbb391d5f5e41ce9a3bbb9ec14c507'
        'f592d84114bbd837854083c0e700b8f0bff2f8c0298418cf2fe612643b732706c7e6a3640c5b78318fb3a3cdbbb8afa2f0fb117fbddd990f603f35fd467592f6'
        '9f0c6e8952b7ad030615489520100a54bd611adf36a352921d6662a9dedfde5f9b030c7ceee2737a22dd2fe2d7d010df69a3bebe146090318c69d0d8c1a9998d'
        '6278877af0df3ba659ff05e37b674325c21593d767d6544680dcdca3e71f2c9b42e604125809a84519ad254e83ce733e1a531b12168ccb4d948707cd1342b628')
validpgpkeys=('ECCAC84C1BA08A6CC8E63FBBF22FB1D78A77AEAB'    # Giancarlo Razzolini
              '86CFFCA918CF3AF47147588051E8B148A9999C34')   # Evangelos Foutras

prepare() {
 cd $_pkgname-$pkgver

 local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

check() {
  make -C "$_pkgname-$pkgver" check
}

package() {
  make -C "$_pkgname-$pkgver" DESTDIR="$pkgdir" install
}
