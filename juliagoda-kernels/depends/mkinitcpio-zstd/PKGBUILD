# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=mkinitcpio-zstd
_pkgname=mkinitcpio
pkgver=29
pkgrel=6
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url="https://projects.archlinux.org/mkinitcpio.git/"
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive' 'coreutils'
         'bash' 'diffutils' 'findutils' 'grep' 'filesystem>=2011.10-1' 'zstd' 'systemd')
makedepends=('asciidoc')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'gzip: Use gzip compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=('initramfs' 'initramfs-zstd' "mkinitcpio=$pkgver")
conflicts=('mkinitcpio')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$_pkgname/$_pkgname-$pkgver.tar.gz"{,.sig}
        "0001-Add-zstd-module-decompression.patch"
        "0002-Set-default-compressor-to-zstd.patch"
        "0003-Allow-user-to-override-default-compression-options.patch"
        "0004-mkinitcpio-Lower-zstd-compression-level-to-15.patch")
install=mkinitcpio.install
sha512sums=('cc03159f16ae12a0d1ebf6a1827f8cdaf73ad0910bf2b1b0f30b737b1cff472130eb41c3669c180291a9861c3c3167498d1f9d60cb9782aeb1ef1d0cd2156068'
            'SKIP'
            '4b2cf42de3acc7beebc23f18e414d8f8df129f0bf04e9b1bd7b03f9747103222d8c9ce3fc71cf71075808354e02cfd8c3caa4d59ffbc37ca7d7b2c475d5a9bd4'
            '97e5353ede9d652bfe78c0b298961870c2eef6b3d26085e212201df24a16316fd4f4eb61f2c224b3594dcee63d13af2ed86845c4458e941aa77cbb5ed11c7d1c'
            'ddb091b27cbb93f245e606acb528c7ea72cecad832426aa5c3e2120cc58d557674949fc99b9bb571d356561e3808fe1ad53e29a81936118a136ef858729d6909'
            'f2aaa3109bd6a9945d61da85c060304d4901ebc5a4772e3c6d1bda62d8389fd7618649c000f9c1e5240afd87ba42f63d1aebfc30c562ed25244c41d6fb0671a0')
b2sums=('4303216cc3c020bf58491a18b13785660e08f418a333f5b975b29b1da4f6b4427f5bbda358018a34e6894217c6368fdf3fe73d361e770a4b6e40abb12ba2fd02'
        'SKIP'
        '08fd9d1e56c8ec46dd190b4004141481a6446bb9cdf0ab2762479f0deb233b9d34bb08d75418fc6aba3030b123bf659a5f5eb7311563117bcc8b0186a0e593a2'
        '6e69260868c5cfa86c4d7628afe8c67a86a763530a4120f41cb2fda2e57aef90d5fff544775d3de40a280f4f0513c667ab13c96beb3be4c71f1fe0e993368d58'
        '5c2ecaa776a447156e3d76cc5b4cc63c69d7d3f6ab0e54c7d3c1587f398bb8bdd2768f29be973d37ca41f5ac3a97818e6965d2e2a221a02f39db16b40957d6b3'
        'c555768c5e4ac38026776d6237a8670f965fc5265bd18035b2222a67e3f35e0fdc6323697cbe893a3b483878b129dc633767c1fb25e09eb65fe405fd11fd6c13')
validpgpkeys=('ECCAC84C1BA08A6CC8E63FBBF22FB1D78A77AEAB'    # Giancarlo Razzolini
              '86CFFCA918CF3AF47147588051E8B148A9999C34')   # Evangelos Foutras

prepare() {
 cd $_pkgname-$pkgver
 # 0001-Add-zstd-module-decompression.patch
 patch -Np1 < $srcdir/0001-Add-zstd-module-decompression.patch
 # 0002-Set-default-compressor-to-zstd.patch
 patch -Np1 < $srcdir/0002-Set-default-compressor-to-zstd.patch
 # 0003-Allow-user-to-override-default-compression-options.patch
 patch -Np1 < $srcdir/0003-Allow-user-to-override-default-compression-options.patch
 # 0004-mkinitcpio-Lower-zstd-compression-level-to-15.patch
 patch -Np1 < $srcdir/0004-mkinitcpio-Lower-zstd-compression-level-to-15.patch
}

check() {
  make -C "$_pkgname-$pkgver" check
}

package() {
  make -C "$_pkgname-$pkgver" DESTDIR="$pkgdir" install
}
