# Maintainer: Piotr Gorski <lucjan.lucjanov@gmail.com>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=mkinitcpio-zstd
_pkgname=mkinitcpio
pkgver=29
pkgrel=3
pkgdesc="Modular initramfs image creation utility"
arch=('any')
url="https://projects.archlinux.org/mkinitcpio.git/"
license=('GPL')
depends=('awk' 'mkinitcpio-busybox>=1.19.4-2' 'kmod' 'util-linux>=2.23' 'libarchive' 'coreutils'
         'bash' 'diffutils' 'findutils' 'grep' 'filesystem>=2011.10-1' 'gzip' 'zstd' 'systemd')
makedepends=('asciidoc')
optdepends=('xz: Use lzma or xz compression for the initramfs image'
            'bzip2: Use bzip2 compression for the initramfs image'
            'lzop: Use lzo compression for the initramfs image'
            'lz4: Use lz4 compression for the initramfs image'
            'mkinitcpio-nfs-utils: Support for root filesystem on NFS')
provides=('initramfs' "mkinitcpio=$pkgver")
conflicts=('mkinitcpio')
backup=('etc/mkinitcpio.conf')
source=("https://sources.archlinux.org/other/$_pkgname/$_pkgname-$pkgver.tar.gz"{,.sig}
        "0001-Add-zstd-module-decompression.patch"
        "0002-Allow-user-to-override-default-compression-options.patch"
        "0003-mkinitcpio-Lower-zstd-compression-level-to-15.patch")
install=mkinitcpio.install
sha512sums=('cc03159f16ae12a0d1ebf6a1827f8cdaf73ad0910bf2b1b0f30b737b1cff472130eb41c3669c180291a9861c3c3167498d1f9d60cb9782aeb1ef1d0cd2156068'
            'SKIP'
            '42b9a6ea2576168619fdd60633099ee18190220b162799c00cb49adb514e182d0e8e9a7a4dfea8c7c593a96aeb3f04bf899b81f36c74fdcf06762c692c818d1b'
            'b7e16346e86e7621b6b60a38ad91b1652c82ea541fc5033f1a7b7635d5032ae6d8e8b40061ba629da040cffd09ad63667f5a226a6bb6b1232b1aa341465db372'
            '77ce086cf9b8924ef18ab9e6d99c80d48be4a53d7a18281a00a589a5a954f2316a5f2a6987d7a0811ec5f8df18c00dfdcc6a22dd7a6bb4b059e02527b81e8425')
b2sums=('4303216cc3c020bf58491a18b13785660e08f418a333f5b975b29b1da4f6b4427f5bbda358018a34e6894217c6368fdf3fe73d361e770a4b6e40abb12ba2fd02'
        'SKIP'
        '02e4c4702da91136ec4df2fc2e8b403244590379ad75d3b07422d0c3c4032c42c635f17e870c2a6a358d628c4a2b023bffb66dc8c53652b8ef51467813dd4206'
        'ff526de2cc670070ee3cd8d41c50be890bf979c533913d22aa3b0f0d9fac8d4afa99742c3bd74f2f050ecb580befb8d2ac5807ea3faea79519f9a3e82e005884'
        '366eb17c0bbb0fed7fc6b95d289a80088996c621db1a0ef9a596784d84ae7ace41adc8edd0dd08a75efaa537748590daf779665320ffa4ee3679e1fda340f956')
validpgpkeys=('ECCAC84C1BA08A6CC8E63FBBF22FB1D78A77AEAB'    # Giancarlo Razzolini
              '86CFFCA918CF3AF47147588051E8B148A9999C34')   # Evangelos Foutras

prepare() {
 cd $_pkgname-$pkgver
 # 0001-Add-zstd-module-decompression.patch
 patch -Np1 < $srcdir/0001-Add-zstd-module-decompression.patch
 # 0002-Allow-user-to-override-default-compression-options.patch
 patch -Np1 < $srcdir/0002-Allow-user-to-override-default-compression-options.patch
 # 0003-mkinitcpio-Lower-zstd-compression-level-to-15.patch
 patch -Np1 < $srcdir/0003-mkinitcpio-Lower-zstd-compression-level-to-15.patch
}

check() {
  make -C "$_pkgname-$pkgver" check
}

package() {
  make -C "$_pkgname-$pkgver" DESTDIR="$pkgdir" install
}
